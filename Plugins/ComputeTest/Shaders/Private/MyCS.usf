#pragma once

#include "/Engine/Private/Common.ush"

float boundaryConditions(uint x, uint y, uint width, uint height )
{
    const float alpha = 0.05f;

    float x0 = float(x) ; float x1 = float(width - x);
    float y0 = float(y) ; float y1 = float(height - y);

    float leftBound, rightBound, topBound, bottomBound;

    leftBound = rightBound = bottomBound = topBound = 0;

    float padX = .1f * width;
    float padY = .1f * height;

    if (x0 >= 0.0f) 
        leftBound = clamp(pow(x0/padX, alpha ),0.0f, 1.0f);
    if (x1 >= 0.0f)
        rightBound = clamp(pow(x1/padX, alpha ),0.0f, 1.0f);
    if (y0 >= 0.0f)
        bottomBound = clamp(pow(y0/padY, alpha),0.0f, 1.0f);
    if (y1 >= 0.0f)
        topBound = clamp(pow(y1/padY, alpha),0.0f, 1.0f);

    return (topBound*bottomBound*rightBound*leftBound);

}

#ifdef ADD_SOURCE_HEIGHTFIELD

#define SRCTYPE float4
#define DSTTYPE float4

#define Texture2DType Texture2D<SRCTYPE>
#define RWTexture2DType RWTexture2D<DSTTYPE>

float4 MyColor;
uint bUseObsMap;
RWTexture2DType OutputSurface;

Texture2DType SrcObstruction;
Texture2DType SrcTexture;
SamplerState TexMapSampler;
RWStructuredBuffer<float4> h0_phi_RW;


//THREADS_PER_GROUP defined in "ComputeTestImplementation.cpp"
[numthreads(THREADS_PER_GROUP,THREADS_PER_GROUP,1)]
void AddSourceHeightCS(uint3 ThreadId : SV_DispatchThreadID)
{
    float4 dstpixel = MyColor;
    
    int width, height;
    SrcTexture.GetDimensions(width, height);
    uint idx = ThreadId.x  + ThreadId.y * width;
    
    //Calculate boundary conditions
    float bound = boundaryConditions(ThreadId.x, ThreadId.y, width, height);

    //Calculate obstruction map
    float4 obsHeight = 0.0f;
    if(bUseObsMap){
        obsHeight = clamp(SrcObstruction[ThreadId.xy],0.0f,1.0f) * -h0_phi_RW[idx];
    }
    float2 uv = float2(ThreadId.xy)/ float2(width * 1.0f, height *1.0f);
    float4 output = SrcTexture[ThreadId.xy];
    float4 pixColor = float4(output.x,0.0f,0.0f,0.0f)*CSVariables.mag*CSVariables.deltaTime;
    
    //Apply Obstruction Maps
    h0_phi_RW[idx] += pixColor + obsHeight;
    
    //Apply Boundary Conditions
    h0_phi_RW[idx] *= bound; 
    OutputSurface[ThreadId.xy] = h0_phi_RW[idx]; 

    return;
}

#endif //ADD_SOURCE_HEIGHTFIELD

#ifdef CALC_EWAVE

#define Texture2DType Texture2D<float4>
#define RWTexture2DType RWTexture2D<float4>

#define GRAV 9.80665f
#define PI 3.1415926535897932f

//Grid Size in meters
float worldGridWidth, worldGridHeight;

// Complex Texture input. 2 complex signals (h and phi): R + iG , B + iA 
Texture2DType In_hT_pT;

RWTexture2DType Out_hT_pT;


[numthreads(THREADS_PER_GROUP, THREADS_PER_GROUP,1)]
void CalcEWaveCS(uint3 ThreadId : SV_DispatchThreadID)
{

    float width, height;
    In_hT_pT.GetDimensions(width, height);
    
    if ((ThreadId.x >= width) || (ThreadId.y >= height)) { return; }

    uint i = ThreadId.x;
    uint j = ThreadId.y;

    //TODO: Need to implement size of Grid in World Size (Lx, Ly)
    float dkx = 2.0f * PI/1.0f; 
    float dky = 2.0f * PI/1.0f;

    float kx, ky;
    if( float(i) <= width/2.0) kx = i * dkx;
    else kx = (i - width) * dkx;

    if (float(j) <= height/2.0) ky = j * dky;
    else ky = (j - height) * dky;

    //float n = ThreadId.x - 256.0f;
    //float m = ThreadId.y - 256.0f;
    uint index = i + j*width;
    //kx = 2 * PI * n/ 512.0f;
    //ky = 2 * PI * m/ 512.0f;

    float mk = sqrt(kx*kx + ky*ky);

    float w = sqrt(GRAV*mk);
    float4 ht_pt = In_hT_pT[ThreadId.xy];
    float coswk, sinwk;
    sincos(w*CSVariables.deltaTime, sinwk, coswk);
    float2 hT, pT;
    if (mk != 0)
    {
        hT.x = coswk*ht_pt.r + (mk / w) * sinwk * ht_pt.b; // h.real
        hT.y = coswk*ht_pt.g + (mk / w) * sinwk * ht_pt.a; // h.imag
        
        pT.x = coswk*ht_pt.b - (GRAV / w) * sinwk * ht_pt.r; // phi.real
        pT.y = coswk*ht_pt.a - (GRAV / w) * sinwk * ht_pt.g; // phi.imag

        //Out_hT_pT[ThreadId.xy] = float4(In_hT_pT[ThreadId.xy].x,In_hT_pT[ThreadId.xy].y, 0.0, 0.0 );

        Out_hT_pT[ThreadId.xy] = float4(hT.x,hT.y, pT.x, pT.y);
    }
}

#endif //CALC_EWAVE

#ifdef APPLY_ADVECTION

#define SRCTYPE float4

#define Texture2DType Texture2D<SRCTYPE>

Texture2DType SrcTexture;
Texture2DType FlowMap;
SamplerState SrcSampler;
//RWStructuredBuffer<float4> h0_phi0_RW;
RWTexture2D<float4> h0_phi0_RW;
uint bUseFlowMap;
float velScale;

[numthreads(THREADS_PER_GROUP, THREADS_PER_GROUP,1)]
void AdvectFieldsCS(uint3 ThreadId : SV_DispatchThreadID)
{
    int width, height;

    SrcTexture.GetDimensions(width, height);
    int idx = ThreadId.x  + ThreadId.y * width;

    
    float4 value = 0.0f;
    if (bUseFlowMap)
    {
        float i = float(ThreadId.x);
        float j = float(ThreadId.y);
        float4 vel = FlowMap[ThreadId.xy]; // takes the R and G values
        //vel = float4(100.0f,0.0f,0.0f,0.0f);
        vel = float4((vel.r - 0.5f)*2.0f, (vel.g - 0.5f) * 2.0f, 0.0f,0.0f);
        // TODO: Need to include dx,dy and Lx,Ly in calculation
        float x = i - vel.r * CSVariables.deltaTime*velScale;
        float y = j - vel.g * CSVariables.deltaTime*velScale;

        if (x < 0) x = 0.5; if (x >= width) x = width - 0.5;
        if (y < 0) y = 0.5; if (y >= height) y = height - 0.5;

        float u = x/width;
        float v = y/height;

         value = SrcTexture.SampleLevel(SrcSampler, float2(u,v),0);
         //value = vel;
    }
    else
    {
        value =  SrcTexture[ThreadId.xy];
    }

    //h0_phi0_RW[idx] = value;
    h0_phi0_RW[ThreadId.xy] = value;
}
#endif //APPLY_ADVECTION


#ifdef GEN_GRAD
Texture2D<float4> RO_Ht_PHIt;

RWTexture2D<float4> dx_dy;
RWTexture2D<float4> RW_Ht_PHIt;

uint genGrad;
uint calcNonLinear;

[numthreads(THREADS_PER_GROUP, THREADS_PER_GROUP,1)]
void GenGradCS(uint3 ThreadId : SV_DispatchThreadID)
{
    uint width, height;

    RO_Ht_PHIt.GetDimensions(width, height);
    int idx = ThreadId.x  + ThreadId.y * width;

    uint i = ThreadId.x;
    uint j = ThreadId.y;

    float2 Ht = RO_Ht_PHIt[ThreadId.xy].rg;
    float2 phi = RO_Ht_PHIt[ThreadId.xy].ba;
    calcNonLinear = false;
    if(calcNonLinear)
    {
        uint idx_left = i - 1;
        uint idx_right = i + 1;

        if(idx_left < 0 ) idx_left = 0;
        if(idx_right >= width) idx_right = width - 1;

        float gradPhiXReal = -(RO_Ht_PHIt[uint2(idx_right, j)].b - RO_Ht_PHIt[uint2(idx_left, j)].b) * 0.5;
        float gradPhiXImag = -(RO_Ht_PHIt[uint2(idx_right, j)].a - RO_Ht_PHIt[uint2(idx_left, j)].a) * 0.5;
        Ht.x  += gradPhiXReal;
        //Ht.y  += gradPhiXImag;
        phi.x += 0.5 * gradPhiXReal;
        //phi.y += 0.5 * gradPhiXImag;
        
    }

    RW_Ht_PHIt[ThreadId.xy] = float4(Ht.x, Ht.y, phi.x, phi.y);
    dx_dy[ThreadId.xy] = 0.0f;
    
    if(genGrad)
    {
        //TODO: Need to implement size of Grid in World Size (Lx, Ly)
        float dkx = 2.0f * PI/1.0f; 
        float dky = 2.0f * PI/1.0f;

        float kx, ky;
        if( float(i) <= width/2.0) kx = i * dkx;
        else kx = (i - width) * dkx;

        if (float(j) <= height/2.0) ky = j * dky;
        else ky = (j - height) * dky;

        float sqr_k = kx*kx + ky*ky;
        float unit_k = 0.0f;
        if(sqr_k > 1e-12f)
        {
            unit_k = 1.0f/sqrt(sqr_k);
        }
        kx *= unit_k;
        ky *= unit_k; 

        //rotated by -PI/2. -i(imaginary component) in fourier space is rotation by -PI/2. Hence -i*(a,b) becomes (b,-a).
        float2 dx = float2(Ht.y * kx, -Ht.x * kx);
        float2 dy = float2(Ht.y * ky, -Ht.x * ky);

        dx_dy[ThreadId.xy] = float4(dx,dy);
    }
}

#endif //GEN_GRAD


#ifdef APPLY_FIELDS

Texture2D<float4> SrcTexture;
Texture2D<float4> ObsTexture;

RWTexture2D<float4> DstTexture;
RWStructuredBuffer<float4> h0_phi0_RW;

Texture2D<float4> dx_dy;

uint bUseObsTexture;

[numthreads(THREADS_PER_GROUP, THREADS_PER_GROUP,1)]
void ApplyFieldsCS(uint3 ThreadId : SV_DispatchThreadID)
{
    int width, height;

    SrcTexture.GetDimensions(width, height);
    int idx = ThreadId.x  + ThreadId.y * width;
    float4 obstructedSrc = SrcTexture[ThreadId.xy];
    float4 obs_dx_dy = dx_dy[ThreadId.xy];

    if (bUseObsTexture)
    {
        float4 obsVal = -ObsTexture[ThreadId.xy] * SrcTexture[ThreadId.xy];
        obstructedSrc += obsVal;

        float obsGradVal = -ObsTexture[ThreadId.xy] * dx_dy[ThreadId.xy];
        obs_dx_dy += obsGradVal;
    }

    //Calculate boundary conditions
    float bound = boundaryConditions(ThreadId.x, ThreadId.y, width, height);
    float4 value = 0.0f;

   // int signCorrection = ((ThreadId.x + ThreadId.y) & 1) ? -1 : 1;
    float signCorrection = 1.0f;
    value = float4(obs_dx_dy.r * CSVariables.choppyScale, obs_dx_dy.b * CSVariables.choppyScale, obstructedSrc.r, 1.0f) * bound;
    DstTexture[ThreadId.xy] = value;
    h0_phi0_RW[idx] = SrcTexture[ThreadId.xy] * bound;
}

#endif //APPLY_FIELDS